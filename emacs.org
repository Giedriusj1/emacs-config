#+TITLE: Emacs config

* PACKAGES
#+BEGIN_SRC emacs-lisp
;;; -*- lexical-binding: t -*-
(message "emacs.org : starting init stage")

(add-to-list 'load-path "~/.emacs.d/elpa")
(add-to-list 'load-path "~/.emacs.d/custom-packages")

(require 'package)
(setq package-archives '(("gnu" . "http://elpa.gnu.org/packages/")
                         ;; ("marmalade" . "http://marmalade-repo.org/packages/")
                         ("melpa" . "http://melpa.milkbox.net/packages/")))

;; not needed since emacs 27
(when (not package--initialized) (package-initialize))

(setq url-http-attempt-keepalives nil)

;; Make sure we have use-package installed.
;; All other packages will be installed by it.
(if (not (package-installed-p 'use-package))
    (progn (package-refresh-contents)
           (package-install 'use-package)))

;; Make sure we install any packages that aren't on the system.
(setq use-package-always-ensure t)
#+end_src

* INIT BASICS
** Garbage collection
#+BEGIN_SRC emacs-lisp
(use-package gcmh
  :config
  (gcmh-mode 1)

  ;; (setq gcmh-verbose t)
  (setq gcmh-time-constant 10)

  ;;Let's garbage collect when focusing out of the window..
  (add-hook 'focus-out-hook #'garbage-collect)
  ;;  and saving files.
  (add-hook 'after-save-hook #'garbage-collect))
#+END_SRC

* LOOKS
#+BEGIN_SRC emacs-lisp
(message "emacs.org : starting looks stage")
#+end_src

** Font helper functions
#+BEGIN_SRC emacs-lisp
(setq current-font "normal")

(defun set-windows-font (mode)
  (cond
   ((string= mode "normal") (set-frame-font "Consolas-10"))
   ((string= mode "medium") (set-frame-font "Consolas-11"))
   ((string= mode "4k") (set-frame-font "Consolas-12"))))

(defun set-linux-font (mode)
  (cond
   ((string= mode "normal") (set-frame-font "Monospace-10"))
   ((string= mode "medium") (set-frame-font "Monospace-12"))
   ((string= mode "4k") (set-frame-font "Monospace-16"))))

(defun set-font (mode)
  (progn
    (setq current-font mode)
    (cond
     ((string-equal system-type "windows-nt")
      ;; We are dealing with Windows sytem
      (set-windows-font mode))
     ((string-equal system-type "gnu/linux")
      ;; We are dealing with linux system:
      (set-linux-font mode)))))

(defun toggle-font-size()
  (interactive)
  (cond
   ((string= current-font "normal")
    (set-font "medium"))
   ((string= current-font "medium")
    (set-font "4k"))
   ((string= current-font "4k")
    (set-font "normal"))))

;; Default font size
(defun set-font-normal ()
  (interactive)
    (set-font "normal"))

;; Larger than normal, but not as big as 4k
(defun set-font-medium ()
  (interactive)
    (set-font "medium"))

;; Sets font size to something that's usable under 4k monitor
(defun set-font-4k ()
  (interactive)
    (set-font "4k"))

(defun is-4k-monitor ()
  (and (<= 2840 (x-display-pixel-width)) (<= 2160 (x-display-pixel-height))))

(defun set-font-for-current-resolution ()
  (interactive)
  (cond
   ((is-4k-monitor) (set-font-4k))
   (t (set-font-normal))))
#+END_SRC

** Themes and colours
#+BEGIN_SRC emacs-lisp
;; On emacs 27 this will be done from early-init.el
(if (< emacs-major-version 27)
    (progn
      (customize-set-variable 'scroll-bar-mode nil)
      (customize-set-variable 'horizontal-scroll-bar-mode nil)
      (customize-set-variable 'menu-bar-mode nil)
      (customize-set-variable 'tool-bar-mode nil)))

(custom-set-variables
 '(custom-safe-themes
   (quote
    ("a27c00821ccfd5a78b01e4f35dc056706dd9ede09a8b90c6955ae6a390eb1c1e"
     "3c83b3676d796422704082049fc38b6966bcad960f896669dfc21a7a37a748fa" default))))

(custom-set-faces
 '(org-level-1 ((t (:inherit outline-1 :weight demibold :foreground "orange3" :height 1.2))))
 '(org-level-2 ((t (:inherit outline-2 :weight demibold :foreground "darkOliveGreen3" :height 1.2))))
 '(org-level-3 ((t (:inherit outline-3 :weight demibold :height 1.1))))
 '(org-level-4 ((t (:inherit outline-4 :weight demibold :height 1.0))))
 '(org-level-5 ((t (:inherit outline-5 :weight demibold :height 1.0)))))


;; Terminal displays are often not happy about certain colours, so we only set them if we are running
;; within a graphic display environment
(if (display-graphic-p)
    (progn
      (custom-set-variables
       '(custom-enabled-themes (quote (wombat))))

      (custom-set-faces
       '(default ((t (:background "#101515"))))
       '(cursor ((t (:background "OrangeRed"))))
       '(mode-line-inactive ((t (:background "#101010"))))
       '(mode-line ((t (:background "#404040"))))
       '(hl-line ((t (:inherit nil :background "#222222"))))
       '(minibuffer-prompt ((t (:foreground "#ff584d"))))

       ;; Make some default wombat colours a bit more lively
       '(font-lock-builtin-face ((((class color) (min-colors 89)) (:foreground "#ff685d"))))
       '(font-lock-constant-face ((((class color) (min-colors 89)) (:foreground "#ff685d"))))

       ;; Some default helm faces are quite ugly... let's fix em up.
       '(helm-selection ((t (:background "grey24" :distant-foreground "black"))))
       '(helm-buffer-directory ((t (:weight bold :foreground "LightSlateBlue" :distant-foreground "black"))))
       '(helm-ff-directory ((t :inherit helm-buffer-directory )))
       '(helm-source-header ((t (:background "#450a6b" :foreground "#dddddd" :weight bold :height 1.3 :family "Sans Serif")))))

      (set-font-for-current-resolution)

      (global-hl-line-mode t)

      (use-package smart-mode-line
        :ensure t
        :config
        (sml/setup))))
#+END_SRC

** Font rendering
 #+BEGIN_SRC emacs-lisp
 (setq-default bidi-display-reordering nil)
 #+END_SRC
** Buffer names
#+BEGIN_SRC emacs-lisp
(use-package uniquify
  :ensure nil
  :config (setq uniquify-buffer-name-style 'forward))
#+END_SRC
** Frame title
#+BEGIN_SRC emacs-lisp
(setq-default frame-title-format
              '(:eval (format "%s@%s: %s %s" (or (file-remote-p default-directory 'user)
                                                 user-real-login-name)
                              (or (file-remote-p default-directory 'host)
                                  system-name)
                              (buffer-name)
                              (cond (buffer-file-truename (concat "(" buffer-file-truename ")"))
                                    (dired-directory (concat "{" dired-directory "}"))
                                    (t "[no file]")))))
#+END_SRC

* BEHAVIOUR
#+BEGIN_SRC emacs-lisp
(message "emacs.org : starting behaviour stage")
#+end_src
** Miscellaneous
*** diminish
#+BEGIN_SRC emacs-lisp
(use-package diminish :config (diminish 'gcmh-mode))
#+END_SRC
*** anzu
#+BEGIN_SRC emacs-lisp
;; Show number of matches in mode-line while searching
(use-package anzu
  :config
  (global-anzu-mode t)
  (diminish 'anzu-mode))
#+END_SRC
*** cua
#+BEGIN_SRC emacs-lisp
(cua-mode 1)
(bind-key "C-f" 'cua-exchange-point-and-mark)

;; (bind-key* "C-c" 'kill-ring-save)
(bind-key* "C-v" 'yank)
#+END_SRC
*** recentf
#+BEGIN_SRC emacs-lisp
(use-package recentf
  :config
  (recentf-mode 1)
  (setq recentf-max-menu-items 100)
  (setq recentf-max-saved-items 100))
#+end_src
*** shell-here
#+BEGIN_SRC emacs-lisp
(use-package shell-here :defer t
  :bind* (( "C-`" . shell-here)))
#+end_src
** Keyboard
*** maps
#+BEGIN_SRC emacs-lisp
(define-prefix-command 'control-semi-map)
(define-prefix-command 'tab-map)

(bind-key* "C-;" 'control-semi-map)
(bind-key* "<tab>" 'tab-map)
(bind-key* "M-;" 'tab-map)
#+end_src

*** global map
#+BEGIN_SRC emacs-lisp
(global-set-key [f9] 'toggle-font-size)
(global-set-key [f10] 'toggle-truncate-lines)
(global-set-key [f11] 'toggle-frame-fullscreen)
(global-set-key [f12] 'whitespace-mode)

(global-set-key (kbd "<Scroll_Lock>") 'scroll-lock-mode)
(global-set-key (kbd "<up>") 'scroll-down-line)
(global-set-key (kbd "<down>") 'scroll-up-line)

(global-set-key (kbd "M-p") 'backward-paragraph)
(global-set-key (kbd "M-n") 'forward-paragraph)

(bind-key* "M-," 'backward-kill-word)
(bind-key* "M-." 'kill-word)

(global-set-key (kbd "M-,") 'backward-kill-word)
(global-set-key (kbd "<down>") 'scroll-up-line)

(global-set-key (kbd "C-d") 'global-superword-mode)
(global-set-key (kbd "C-M-SPC") 'rectangle-mark-mode)

(bind-key* "C-," 'delete-backward-char)
(bind-key* "C-." 'delete-char)

(bind-key* "M-h" 'open-line)

(bind-key* "C-u" 'backward-char)
(bind-key* "C-o" 'forward-char)

(bind-key* "M-u" 'backward-word)
(bind-key* "M-o" 'forward-word)

(bind-key* "C-M-u" 'backward-sexp)
(bind-key* "C-M-o" 'forward-sexp)

(bind-key* "C-d" 'Control-X-prefix)
(bind-key* "C-a" 'Control-X-prefix)

(bind-key* "C-q" 'beginning-of-line)
(bind-key* "C-w" 'back-to-indentation)
#+end_src

*** ctl-x-map
#+BEGIN_SRC emacs-lisp
(define-key ctl-x-map "\C-f" 'helm-find-files)
(define-key ctl-x-map "\C-d" 'dired-jump)
#+end_src
*** control-semi-map
#+BEGIN_SRC emacs-lisp
(define-key control-semi-map (kbd "SPC") 'point-to-register)
(define-key control-semi-map (kbd "C-SPC") 'point-to-register)
(define-key control-semi-map (kbd "j") 'jump-to-register)
(define-key control-semi-map (kbd "h") 'highlight-phrase)
(define-key control-semi-map (kbd "q") 'goto-line)

(define-key control-semi-map (kbd "C-j") 'jump-to-register)
(define-key control-semi-map (kbd "C-q") 'goto-line)
(define-key control-semi-map (kbd "C-l") 'execute-extended-command)
(define-key control-semi-map (kbd "C-2") 'split-window-below)

(define-key control-semi-map (kbd "C-2") '(lambda ()
                                            (interactive)
                                            (split-window-below)
                                            (balance-windows)))

(define-key control-semi-map (kbd "C-3") '(lambda ()
                                            (interactive)
                                            (split-window-right)
                                            (balance-windows)))


(define-key control-semi-map (kbd "C-0") '(lambda ()
                                            (interactive)
                                            (delete-window)
                                            (balance-windows)))

(define-key control-semi-map (kbd "C-4") 'balance-windows)

(define-key control-semi-map (kbd "C-d") 'follow-mode)
#+end_src
*** tab map
#+BEGIN_SRC emacs-lisp
(define-key tab-map (kbd "TAB") 'comment-dwim)
(define-key tab-map (kbd "M-;") 'comment-dwim)
(define-key tab-map (kbd "u") 'universal-argument)
#+end_src
*** windmove + frame selection
#+BEGIN_SRC emacs-lisp
(setq windmove-wrap-around t)

(use-package zygospore
  :bind* (("C-1" . 'window-swap-states)
          ("C-2" . 'windmove-up)
          ("C-3" . 'windmove-right)
          :map control-semi-map
          ("C-1" . zygospore-toggle-delete-other-windows)))
#+end_src
** hydra
#+BEGIN_SRC emacs-lisp
(use-package hydra :ensure t :defer t)

(defun spawn-local-mode-hydra ()
  (interactive)
  (cond (( string= "org-mode" major-mode)
         (hydra-tab-org/body))
        (( string= "c-mode" major-mode)
         (hydra-c/body))
        (( string= "c++-mode" major-mode)
         (hydra-c/body))
        (( string= "python-mode" major-mode)
         (hydra-python/body))
        (( string= "emacs-lisp-mode" major-mode)
         (hydra-emacs-lisp/body))
         (( string= "scala-mode" major-mode)
         (hydra-scala/body))
        (t (message "Argh...hydra for your current mode does not exist :("))))

;; Needed for helm-find-files-1
(use-package helm-files :defer t :ensure nil)

(defhydra hydra-quickopen (:color blue)
  "
[_t_] ~/notes/temp
[_c_] ~/.emacs.d/emacs.org
[_;_] org rifle
[_l_] dired ~/.emacs.d
"
  ("t" (lambda ()
         (interactive)
         (find-file "~/emacs-temp")) nil)
  ("c" (lambda ()
         (interactive)
         (find-file "~/.emacs.d/emacs.org")) nil)
  (";" helm-org-rifle-important nil)
  ("l" (lambda ()
         (interactive)
         (progn
           (zygospore-toggle-delete-other-windows)
           (dired "~/org-notes")
           (helm-find-files-1 default-directory))) nil))

(define-key tab-map (kbd "j") 'spawn-local-mode-hydra)
(define-key tab-map (kbd "m") 'hydra-magit/body)
(define-key tab-map (kbd ";") 'hydra-quickopen/body)

(define-key tab-map (kbd "o") 'hydra-search-helper/body)
(define-key tab-map (kbd "f") 'hydra-frame-helper/body)

(defhydra hydra-search-helper
  (:color blue)
  "
[_q_] update tags        [_o_] find gtag
[_c_] create gtag        [_p_] hydra-lsp
 " ("q" ggtags-update-tags nil)
 ("c" ggtags-create-tags nil)
 ("o" ggtags-find-tag-dwim nil)
 ("p" hydra-lsp/body nil))

(defhydra hydra-frame-helper
  (:color blue)
  "
[_m_] make frame        [_o_] other frame        [_d_] delete frame
 "
  ("m" make-frame nil)
  ("o" other-frame nil)
  ("d" delete-frame nil))
#+END_SRC
** elfeed
#+BEGIN_SRC emacs-lisp
(setq elfeed-feeds
      '("http://nullprogram.com/feed/"
        "http://planet.emacsen.org/atom.xml"
        "https://www.spacerogue.net/wordpress/?feed=rss2"
        "https://mjg59.dreamwidth.org/data/rss"))
#+END_SRC
** Multiple cursors
#+BEGIN_SRC emacs-lisp
(use-package multiple-cursors :defer t)

(defhydra hydra-multiple-cursors (:hint nil)
  "
 Up^^             Down^^           Miscellaneous           % 2(mc/num-cursors) cursor%s(if (> (mc/num-cursors) 1) \"s\" \"\")
------------------------------------------------------------------
 [_p_]   Next     [_n_]   Next     [_l_] Edit lines  [_0_] Insert numbers
 [_P_]   Skip     [_N_]   Skip     [_a_] Mark all    [_A_] Insert letters
 [_M-p_] Unmark   [_M-n_] Unmark   [_s_] Search
 [Click] Cursor at point       [_q_] Quit"
  ("l" mc/edit-lines :exit t)
  ("a" mc/mark-all-like-this :exit t)
  ("n" mc/mark-next-like-this)
  ("N" mc/skip-to-next-like-this)
  ("M-n" mc/unmark-next-like-this)
  ("p" mc/mark-previous-like-this)
  ("P" mc/skip-to-previous-like-this)
  ("M-p" mc/unmark-previous-like-this)
  ("s" mc/mark-all-in-region-regexp :exit t)
  ("0" mc/insert-numbers :exit t)
  ("A" mc/insert-letters :exit t)
  ("<mouse-1>" mc/add-cursor-on-click)
  ;; Help with click recognition in this hydra
  ("<down-mouse-1>" ignore)
  ("<drag-mouse-1>" ignore)
  ("q" nil))

(define-key tab-map (kbd "M-m") 'hydra-multiple-cursors/body)

#+END_SRC

** projectile
#+BEGIN_SRC emacs-lisp
(use-package helm-projectile :defer t
  :bind (:map tab-map
              ("p" . hydra-projectile/body))
  :init
  (projectile-global-mode t)
  (diminish 'projectile-mode)

  :config
  (remove-hook 'find-file-hook #'projectile-find-file-hook-function)

  ;; Make projectiel use external tools for file indexing.
  ;; If this breaks revert to 'native for more reliability.
  (setq projectile-indexing-method 'alien)

  (defcustom g/helm-source-projectile-projects-actions
    (helm-make-actions "Open Dired in project's directory `C-d'" #'dired "Switch to project"
                       (lambda (project)
                         (let ((projectile-completion-system 'helm))
                           (projectile-switch-project-by-name
                            project)))
                       "Open project root in vc-dir or magit `M-g'" #'helm-projectile-vc
                       "Switch to Eshell `M-e'" #'helm-projectile-switch-to-eshell
                       "Grep in projects `C-s'" #'helm-projectile-grep
                       "Compile project `M-c'. With C-u, new compile command"
                       #'helm-projectile-compile-project "Remove project(s) from project list `M-D'"
                       #'helm-projectile-remove-known-project)
    "Actions for `helm-source-projectile-projects'."
    :group 'helm-projectile
    :type '(alist :key-type string
                  :value-type function))

  (defvar g/helm-source-projectile-projects
    (helm-build-sync-source "Projectile projects"
      :candidates (lambda ()
                    (with-helm-current-buffer projectile-known-projects))
      :keymap helm-projectile-projects-map
      :mode-line helm-read-file-name-mode-line-string
      :action 'g/helm-source-projectile-projects-actions)
    "Helm source for known projectile projects.")

  (defun helm-projectile-projects ()
    (interactive)
    (let ((helm-ff-transformer-show-only-basename nil))
      (helm :sources '(g/helm-source-projectile-projects)
            :buffer "*helm projectile projects*"
            :truncate-lines helm-projectile-truncate-lines)))

  (customize-set-variable 'helm-projectile-sources-list '(helm-source-projectile-buffers-list
                                                          helm-source-projectile-files-list))

  (defhydra hydra-projectile
    (:color blue)
    "
[_q_] invalidate cache [_p_] projects
[_j_] helm projectile  [_d_] dired projectile root
[_g_]rep [_m_] ag [_a_]ck
" ("p" helm-projectile-projects nil)
("q" projectile-invalidate-cache nil)
("j" helm-projectile nil)
("d" projectile-dired nil)
("g" helm-projectile-grep nil)
("a" helm-projectile-ack nil)
("m" helm-projectile-ag nil)))
#+END_SRC
** dired
#+BEGIN_SRC emacs-lisp
(use-package dired-extension :ensure nil)

(setq dired-dwim-target t)

(define-key dired-mode-map (kbd "l") 'dired-up-directory)
(define-key dired-mode-map (kbd "r") 'dired-do-redisplay)

(setq dired-listing-switches "-alFh")

(when (memq system-type '(gnu gnu/linux))
  (setq dired-listing-switches
        (concat dired-listing-switches " --group-directories-first -v")))

(defun open-in-external-app ()
  (interactive)
  (let ((fileList (cond ((string-equal major-mode "dired-mode")
                           (dired-get-marked-files))
                          (t (list (buffer-file-name))))))
    (cond ((string-equal system-type "windows-nt")
           (mapc (lambda (path) (w32-shell-execute "open" (replace-regexp-in-string "/" "\\" path t t))) fileList))
          ((string-equal system-type "darwin")
           (mapc (lambda (path) (shell-command (format "open \"%s\"" path))) fileList))
          ((string-equal system-type "gnu/linux")
           (mapc (lambda (path) (let ((process-connection-type nil)) (start-process "" nil "xdg-open" path))) fileList)))))
#+end_src

** ORG mode
#+BEGIN_SRC emacs-lisp
(defhydra hydra-tab-org (:color blue)
  "
 [_o_]   metaright   [_u_]   metaleft  [_n_]   metaup  [_p_]   metadown
 [_C-o_] shiftright  [_C-u_] shiftleft [_C-n_] shiftup [_C-p_] shiftdown
 [_e_]   edit source [_s_] exit source edit buffer
 [_c_]   yas helm expand

  "
  ( "o" org-metaright nil)
  ( "u" org-metaleft nil)
  ( "p" org-metaup nil)
  ( "n" org-metadown nil)

  ( "C-o" org-shiftright nil)
  ( "C-u" org-shiftleft nil)
  ( "C-p" org-shiftup nil)
  ( "C-n" org-shiftdown nil)

  ( "e" org-edit-src-code nil)
  ( "s" org-edit-src-exit nil)

  ( "c" helm-yas-complete nil))

(setq org-directory "~/org-notes")
(setq org-src-fontify-natively t)
(setq org-src-preserve-indentation t)
(setq org-startup-indented t)
(setq org-startup-truncated nil)
(setq org-export-with-toc nil)
(setq org-hierarchical-todo-statistics nil)
(setq org-imenu-depth 5)
#+END_SRC
*** org-rifle
#+BEGIN_SRC emacs-lisp
(use-package helm-org-rifle :defer t)

;; Recursive search performs quite poorly on Windows systems
(setq helm-org-rifle-directories-recursive nil)

(defun helm-org-rifle-important ()
  "Rifle through Org files in the directories below"
  (interactive)
  (helm-org-rifle-directories (list
                               "~/org-notes"
                               "~/org-notes/lang-notes"
                               "~/org-notes-private"
                               "~/.emacs.d")))
#+END_SRC
*** org-notes synching
**** git push/pull timer
#+BEGIN_SRC emacs-lisp
(defun org-notes-synch-fn ()
(interactive)
  (let* ((default-directory "~/org-notes"))
	(message "synching org notes with git repo")
	(start-process "proc-git-status" "notes-sync-output" "git" "status")
	(sit-for 5)
	(start-process "proc-git-pull" "notes-sync-output" "git" "pull")
	(sit-for 5)
	(start-process "proc-git-push" "notes-sync-output" "git" "push")))

;; Run the above every hour (if we are idle)
(run-with-idle-timer (* 60 60) t 'org-notes-synch-fn)
#+END_SRC
**** git-auto-commit
#+BEGIN_SRC emacs-lisp
(use-package git-auto-commit-mode :defer t)
#+END_SRC
** Misc behaviour
#+BEGIN_SRC emacs-lisp
;; set to t to investigate crashes
(setq debug-on-error nil)
(setq inhibit-splash-screen t)
(setq initial-scratch-message "")
(setq column-number-mode t)
(setq history-length 25)
(setq select-enable-clipboard t) ;; Merge OS and Emacs' clipboards

(setq auto-window-vscroll nil)   ;; Gives us better line scrolling performance

;; We'll ask emacs to put all customizations made via it's customize package in a
;; separate file... so we can ignore it later :)
(setq custom-file (concat user-emacs-directory "/custom--ignored.el"))

(blink-cursor-mode -1)
(use-package auto-highlight-symbol
  :config (global-auto-highlight-symbol-mode 1))

(delete-selection-mode 1)
(show-paren-mode t)

;; Make the interface a bit more snappy
(setq idle-update-delay 0.1)

(which-function-mode 1)
(custom-set-faces '(which-func ((t (:foreground "LightSlateBlue")))))

(customize-set-variable 'electric-pair-mode t)
(customize-set-variable 'bmkp-last-as-first-bookmark-file "~/.emacs.d/bookmarks" )

(setq backup-by-copying t      ; don't clobber symlinks
      backup-directory-alist
      '(("." . "~/.saves"))    ; don't litter my fs tree
      delete-old-versions t
      kept-new-versions 6
      kept-old-versions 2
      version-control t)       ; use versioned backups

(defun my-create-non-existent-directory ()
  (let ((parent-directory (file-name-directory buffer-file-name)))
    (when (and (not (file-exists-p parent-directory))
               (y-or-n-p (format "Directory `%s' does not exist! Create it?" parent-directory)))
      (make-directory parent-directory t))))

(add-to-list 'find-file-not-found-functions #'my-create-non-existent-directory)

(use-package google-this)

(defadvice text-scale-increase (around all-buffers (arg) activate)
  (dolist (buffer (buffer-list))
    (with-current-buffer buffer
      ad-do-it)))

(setq ring-bell-function 'ignore)
#+end_src
** Programming
*** finding things
**** ag
#+BEGIN_SRC emacs-lisp
(use-package ag :defer t)
#+END_SRC
**** tags
#+BEGIN_SRC emacs-lisp
(use-package ggtags :defer t
  :config
  ;; This should prevent Emacs from asking "Keep current list of tags tables also?"
  (setq tags-add-tables nil)

  ;; Prevent ggtags mode from displaying project name in mode line.
  ;; Projectile already displays this information.
  (setq ggtags-mode-line-project-name nil))
#+end_src
*** code completion
**** yas
#+BEGIN_SRC emacs-lisp
(use-package yasnippet-snippets :defer t)
(use-package yasnippet
  :init
  (yas-global-mode 1)
  (diminish 'yas-minor-mode))

(use-package helm-c-yasnippet :defer t)
#+END_SRC

**** company
#+BEGIN_SRC emacs-lisp
(use-package company-lsp :defer t)
(use-package company
  :bind (:map company-active-map
              (("C-n" . company-select-next)
               ("C-p" . company-select-previous))
              :map control-semi-map
              (("n" . company-complete)
               ("C-n" . dabbrev-expand)))
  :config
  (global-company-mode t)
  (diminish 'company-mode)

  (push 'company-lsp company-backends)

  ;; (add-to-list 'company-backends '(company-clang))
  (add-to-list 'company-backends '(company-gtags))

  (setq company-tooltip-limit 25))
#+end_src
*** gdb
#+BEGIN_SRC emacs-lisp
(define-key tab-map (kbd "h") 'hydra-gdb-helper/body)

(defhydra hydra-gdb-helper (:color blue)
  ( "h" gdb-restore-windows "restore gdb windows")
  ( "m" gdb-many-windows "many windows"))
#+end_src
*** semantic
#+BEGIN_SRC emacs-lisp
(semantic-mode 1) ;; global mode

;; This effectively disables idle reparsing for all files
(setq semantic-idle-scheduler-max-buffer-size 1)

;; We don't care about saving db when exiting emacs
(remove-hook 'kill-emacs-hook #'semanticdb-kill-emacs-hook)

(defun ds () t)
(add-hook 'semantic-inhibit-functions  #'ds)
#+END_SRC
*** code formatting
#+BEGIN_SRC emacs-lisp
(use-package clang-format :defer t
  :config
  ;; The following somewhat resembles Resilient's coding style
  (setq clang-format-style "{BasedOnStyle: google, ColumnLimit: 100, IndentWidth: 3, BreakBeforeBraces: Stroustrup}"))

(use-package elisp-format :defer t)

(define-key tab-map (kbd "i")
  '(lambda ()
     (interactive)
     (cond ((or ( string= "c++-mode" major-mode)
                ( string= "c-mode" major-mode))
            (if (use-region-p)
                (clang-format-region (region-beginning)
                                     (region-end) )
              (clang-format-region (point)
                                   (point))))
           (( string= "emacs-lisp-mode" major-mode)
            (elisp-format-region))
           (t (message "Argh...don't know how to format in this mode :(")))))

#+END_SRC
*** indenting
#+BEGIN_SRC emacs-lisp
(setq-default c-basic-offset 3 c-default-style "linux")
(setq-default tab-width 3 indent-tabs-mode nil)
#+end_src
*** Programming languages
**** C/C++ common
#+BEGIN_SRC emacs-lisp
(defhydra hydra-c (:color blue)
  ( "c" helm-yas-complete "helm yas complete"))

(add-hook 'c-mode-common-hook
          (lambda()
            ;; Use C++ style comments
            (setq comment-start "//"
                  comment-end   "")))
#+end_src

**** Python
#+BEGIN_SRC emacs-lisp
(add-hook 'python-mode-hook
      (lambda()
         (setq indent-tabs-mode nil)
         (setq python-indent 4)
         (setq tab-width 4)))

(defhydra hydra-python (:color blue)
  ( "c" helm-yas-complete "helm yas complete"))
#+end_src

**** Scheme
#+BEGIN_SRC emacs-lisp
(add-hook 'scheme-mode-hook
      (lambda()
         (setq indent-tabs-mode nil)))
#+end_src

**** emacs-lisp
#+BEGIN_SRC emacs-lisp
(defhydra hydra-emacs-lisp (:color blue)
  ( "j" eval-buffer "eval buffer")
  ( "k" eval-last-sexp "eval-last-sexp")
  ( "c" helm-yas-complete "yas complete"))
#+end_src
**** Scala
#+BEGIN_SRC emacs-lisp
(use-package scala-mode :defer t
  :config
  (defhydra hydra-scala (:color blue)
    ( "c" helm-yas-complete "yas complete")))

;; (add-hook 'scala-mode-hook #'lsp)

#+END_SRC
**** java
#+BEGIN_SRC emacs-lisp
(use-package lsp-java)

;; move somewhere else
(use-package lsp-ui)

;; (add-hook 'java-mode-hook #'lsp)

(defhydra hydra-lsp (:exit t :hint nil)
  "
 Buffer^^               Server^^                   Symbol
-------------------------------------------------------------------------------------
 [_f_] format           [_M-r_] restart            [_d_] declaration  [_i_] implementation  [_o_] documentation
 [_m_] imenu            [_S_]   shutdown           [_D_] definition   [_t_] type            [_r_] rename
 [_x_] execute action   [_M-s_] describe session   [_R_] references   [_s_] signature"
  ("d" lsp-find-declaration)
  ("D" lsp-ui-peek-find-definitions)
  ("R" lsp-ui-peek-find-references)
  ("i" lsp-ui-peek-find-implementation)
  ("t" lsp-find-type-definition)
  ("s" lsp-signature-help)
  ("o" lsp-describe-thing-at-point)
  ("r" lsp-rename)

  ("f" lsp-format-buffer)
  ("m" lsp-ui-imenu)
  ("x" lsp-execute-code-action)

  ("M-s" lsp-describe-session)
  ("M-r" lsp-restart-workspace)
  ("S" lsp-shutdown-workspace))
#+END_SRC
*** Structured formats
**** YAML
#+BEGIN_SRC emacs-lisp
(use-package yaml-mode :defer t)
#+END_SRC
**** SGML [XML/HTML]
#+BEGIN_SRC emacs-lisp
(setq nxml-child-indent 4 nxml-attribute-indent 4)

(defun reformat-xml ()
  (interactive)
  ;;todo: this only works in xml-mode, we should spit out an error if we are not

  (save-excursion
    (sgml-pretty-print (point-min) (point-max))
    (indent-region (point-min) (point-max))))
#+END_SRC
**** LDIF
#+BEGIN_SRC emacs-lisp
(use-package ldap-mode :ensure nil)
#+END_SRC

** Verson Control
*** magit
#+BEGIN_SRC emacs-lisp
(use-package magit :defer t)

(defhydra hydra-magit (:color blue)
  "magit"
  ("m" magit-status "status")
  ("p" magit-pull "pull")
  ("P" magit-push "push")
  ("c" magit-commit "commit")
  ("l" magit-log "log")
  ("d" magit-diff-dwim "diff-dwim")
  ("D" magit-diff "diff")
  ("b" magit-blame "blame"))
#+end_src

*** ediff
#+BEGIN_SRC emacs-lisp
(use-package ediff :defer t
  :config
  ;; Setting this to t will only show two panes.
  ;; This set to nil can be useful when dealing wih merge conflicts.
  (setq magit-ediff-dwim-show-on-hunks t)

  ;; turn off whitespace checking:
  (setq ediff-diff-options "-w")

  ;; Don't spawn new window for ediff
  (setq ediff-window-setup-function 'ediff-setup-windows-plain)

  ;; split window horizontally
  (setq ediff-split-window-function 'split-window-horizontally)

  ;; Since edif colours really don't play nicely with dark themes, we'll just overload them
  ;; with magit colours. (This hack is taken from https://github.com/bbatsov/solarized-emacs/issues/194)
  (dolist (entry '((ediff-current-diff-C . ((((class color) (background light))
                                             (:background "#DDEEFF" :foreground "#005588"))
                                            (((class color) (background dark))
                                             (:background "#005588" :foreground "#DDEEFF"))))
                   (ediff-fine-diff-C . ((((class color) (background light))
                                          (:background "#EEFFFF" :foreground "#006699"))
                                         (((class color) (background dark))
                                          (:background "#006699" :foreground "#EEFFFF"))))))
    (let ((face (car entry))
          (spec (cdr entry)))
      (put face 'theme-face nil)
      (face-spec-set face spec)))

  (use-package magit) ;; Needed for all magit-* stuff below
  (dolist (face-map '((ediff-even-diff-A           . magit-diff-context-highlight)
                      (ediff-even-diff-Ancestor    . magit-diff-context)
                      (ediff-even-diff-B           . magit-diff-context-highlight)
                      (ediff-even-diff-C           . magit-diff-context-highlight)
                      (ediff-odd-diff-A            . magit-diff-context-highlight)
                      (ediff-odd-diff-Ancestor     . magit-diff-context)
                      (ediff-odd-diff-B            . magit-diff-context-highlight)
                      (ediff-odd-diff-C            . magit-diff-context-highlight)
                      (ediff-current-diff-A        . magit-diff-our)
                      (ediff-current-diff-Ancestor . magit-diff-base)
                      (ediff-current-diff-B        . magit-diff-their)
                      (ediff-fine-diff-A           . magit-diff-removed-highlight)
                      (ediff-fine-diff-Ancestor    . magit-diff-base-highlight)
                      (ediff-fine-diff-B           . magit-diff-added-highlight)))
    (let* ((face (car face-map))
           (alias (cdr face-map)))
      (put face 'theme-face nil)
      (put face 'face-alias alias)))

  ;; This makes ediff usable with org mode
  (with-eval-after-load 'outline
    (add-hook 'ediff-prepare-buffer-hook #'outline-show-all)))
#+end_src
** Mode recognition
#+BEGIN_SRC emacs-lisp
(setq auto-mode-alist
      '(("[Mm]ake[Ff]ile\\'" . makefile-mode)
        ("\\.mak\\'" . makefile-mode)
        ("\\.md\\'" . markdown-mode)
        ("\\.notes$" . org-mode)
        ("\\.org$" . org-mode)
        ("\\.org.gpg$" . org-mode)
        ("\\.pdf\\'" . doc-view-mode)
        ("\\.ref$" . org-mode)
        ("\\.ref.gpg$" . org-mode)
        ("\\.xml\\'" . xml-mode)
        ("\\.pom\\'" . xml-mode)
        ("\\.ldif\\'" . ldif-mode)

        ;;programming modes
        ("\\.bat\\'" . bat-mode)
        ("\\.c\\'" . c-mode)
        ("\\.cc\\'" . c-mode)
        ("\\.cmd\\'" . bat-mode)
        ("\\.cpp\\'" . c++-mode)
        ("\\.el\\'" . emacs-lisp-mode)
        ("\\.h\\'" . c++-mode)
        ("\\.hh\\'" . c++-mode)
        ("\\.hpp\\'" . c++-mode)
        ;; ("\\.hs$" . haskell-mode)
        ("\\.java\\'" . java-mode)
        ("\\.mc\\'" . c++-mode)
        ("\\.pm\\'" . perl-mode)
        ("\\.py\\'" . python-mode)
        ("\\.rs\\'" . rust-mode)
        ("\\.scala\\'" . scala-mode)
        ("\\.scm\\'" . scheme-mode)
        ("\\.sh\\'" . sh-mode)
        ("\\.yml\\'" . yaml-mode)
        ("\\.s\\'" . asm-mode)
        ("\\.S\\'" . asm-mode)))
#+end_src

** Navigating around
*** Helm
#+BEGIN_SRC emacs-lisp
(use-package asm-mode :defer t
  :bind (:map asm-mode-map
              ("C-j" . helm-mini)))

(defun g/helm-semantic-or-imenu (arg)
  (interactive "P")
  (remove-hook 'semantic-inhibit-functions #'ds)
  (semantic-new-buffer-fcn)
  (helm-semantic-or-imenu arg)
  (add-hook 'semantic-inhibit-functions  #'ds))

(use-package helm
  :bind
  (("C-j" . helm-mini))
  (:map control-semi-map
        (( "C-s" . g/helm-semantic-or-imenu)
         ( "l" . helm-M-x)
         ( "r" . helm-mark-ring)
         ( "C-r" . helm-global-mark-ring)
         ( "b" . helm-resume)
         ( "C-b" . helm-resume)))
  (:map org-mode-map (("C-j" . helm-mini)) )
  (:map lisp-interaction-mode-map (("C-j" . helm-mini)))
  :config
  (setq helm-candidate-number-limit 500)
  (setq helm-buffer-max-length 60))

(use-package swiper :defer t)
(use-package swiper-helm
  :bind (:map control-semi-map (())
              ("o" . swiper-helm)
              ("C-;" . swiper-helm)))

(use-package helm-swoop
  :bind (:map control-semi-map
              (("C-m" . helm-swoop)
               ("m" . helm-multi-swoop-all))))
#+end_src
*** SWIFT
#+BEGIN_SRC emacs-lisp
(defun swift-up(&optional arg)
  (interactive)
  (or arg (setq arg 1))
  (dotimes (bind arg)
    (scroll-down-line)
    (previous-line)))

(defun swift-down(&optional arg)
  (interactive)
  (or arg (setq arg 1))
  (dotimes (bind arg)
    (scroll-up-line)
    (next-line)))

(define-key control-semi-map (kbd "C-f") 'toggle-swift-mode)

(defvar swift-command-map
  (let ((map (make-sparse-keymap)))
    ;; movement
    (define-key map (kbd "i") '(lambda ()
                                 (interactive)
                                 (swift-up 2)))

    (define-key map (kbd "k") '(lambda ()
                                 (interactive)
                                 (swift-down 2)))

    (define-key map (kbd "o") 'swift-up)
    (define-key map (kbd "l") 'swift-down)

    (define-key map (kbd "p") 'beginning-of-defun)
    (define-key map (kbd "n") 'end-of-defun)

    (define-key map (kbd "u") 'cua-scroll-down)
    (define-key map (kbd "j") 'cua-scroll-up)

    ;; cua mode
    (define-key map (kbd "C-z") 'toggle-swift-mode)
    (define-key map (kbd "C-x") 'kill-region)
    (define-key map (kbd "C-c") 'kill-ring-save)
    (define-key map (kbd "C-v") 'yank)
    map))

(define-minor-mode swift-mode
  "Toggle SWIFT buffer mode."
  ;; The initial value.
  :init-value nil
  ;; The indicator for the mode line.
  :lighter " SWIFT"
  ;; The minor mode bindings.
  :keymap swift-command-map)

(define-globalized-minor-mode global-swift-mode swift-mode
  swift-mode
  :init-value nil)

(defun toggle-swift-mode()
  (interactive)
  (if (eq global-swift-mode t)
      (progn ;; turning mode off
        (custom-set-faces '(cursor ((t (:background "OrangeRed")))))
        (custom-set-faces '(mode-line ((t (:background "#404040")))))
        (global-swift-mode -1))

    (progn ;; turning mode off
      (custom-set-faces '(cursor ((t (:background "blue")))))
      (custom-set-faces '(mode-line ((t (:background "#333377")))))
      (global-swift-mode))))
#+end_src

*** eyebrowse
#+BEGIN_SRC emacs-lisp
(use-package eyebrowse :ensure t
  :config
  (setq eyebrowse-mode-line-separator " " eyebrowse-new-workspace t)
  (eyebrowse-mode t))

(defhydra hydra-eyebrowse (:color red)
  "
  ^
  %s(eyebrowse-mode-line-indicator)

  _u_ left window config
  _o_ right window config

  _r_ename current window config
  _c_reate new window config
  _C_lose current window config
  ^^
  "
  ("q" nil "quit")
  ("u" eyebrowse-prev-window-config nil)
  ("o" eyebrowse-next-window-config nil)
  ("r" eyebrowse-rename-window-config nil)
  ("c" eyebrowse-create-window-config nil)
  ("C" eyebrowse-close-window-config nil))

(define-key global-map [(f5)] 'hydra-eyebrowse/body)
#+END_SRC
** Utility functions
#+BEGIN_SRC emacs-lisp
(defun recompile-custom-packages ()
  (interactive)
  (byte-recompile-directory (expand-file-name "~/.emacs.d/custom-packages") 0))

(defun reload-emacs-config ()
  (interactive)
  (load-file "~/.emacs.d/init.el"))

(defun org-babel-reload-emacs-org()
  (interactive)
  (org-babel-load-file "~/.emacs.d/emacs.org"))

(defun emacs-init-time ()
  "Return a string giving the duration of the Emacs initialization."
  (interactive)
  (let ((str (format "%.2f seconds"
                     (float-time (time-subtract after-init-time before-init-time)))))
    (if (called-interactively-p 'interactive)
        (message "%s" str) str)))

(defun display-startup-echo-area-message ()
  (message (concat "Emacs took " (emacs-init-time) " seconds to start.")))
#+end_src

* ALIAS
#+BEGIN_SRC emacs-lisp
(message "emacs.org : starting alias stage")

(defalias 'yes-or-no-p 'y-or-n-p)
(defalias 'describe-bindings 'helm-descbinds)

(defalias 'rel 'reload-emacs-config)
(defalias 'lp 'list-packages)
(defalias 'hlp 'helm-list-elisp-packages-no-fetch)
(defalias 'igf 'igrep-find)
(defalias 'msf 'menu-set-font)
(defalias 'obr 'org-babel-reload-emacs-org)

(message "emacs.org : done loading!")
#+end_src
